//@version=6
// myPj/tradingview/pine/indicators/pjdhiro_trend_indicator.pine
indicator("トレンド判定（SMA200・緩やか）", overlay=true, shorttitle="Trend SMA200")

// --- import ---
import uminomae/pjdhiro_trend_core_v6/3 as core

// --- const（SMA200用・緩やか前提） ---
const string VER              = " v6.sma200"
const int    DEF_LB           = 21
const int    DEF_LEN_ATR      = 200
const float  DEF_EPS_PPB      = 2.0
const float  DEF_THETA_NORM   = 0.04
const float  DEF_HYST         = 1.5
const color  DEF_UP_COLOR     = color.teal
const color  DEF_DOWN_COLOR   = color.red
const color  DEF_FLAT_COLOR    = color.orange
const color  DEF_SMA_COLOR    = color.orange
const int    DEF_SMA_WIDTH    = 2

// --- input（最小） ---
int    lb          = input.int(DEF_LB,        "Slope lookback (bars)"+VER, minval=1)
string modeStr     = input.string("ATR正規化", "Threshold mode"+VER, options=["符号＋フラット帯","ATR正規化"])
float  epsPpb      = input.float(DEF_EPS_PPB,   "±Flat帯 (pips/bar) [符号]",     minval=0.0)
int    lenAtr      = input.int(DEF_LEN_ATR,     "ATR length [正規化]",           minval=1)
float  thetaNorm   = input.float(DEF_THETA_NORM,"Min norm (slope/ATR) [正規化]", minval=0.0)
bool   confirmedOn = input.bool(true,           "確定足のみで判定"+VER)
float  hyst        = input.float(DEF_HYST,      "ヒステリシス係数 (戻り厳しめ)",  minval=1.0)

// --- calc（libは純関数。状態はmain） ---
int  modeId = modeStr == "符号＋フラット帯" ? 0 : 1
[isUp, isDown, isFlat, slopeP, norm, maNow] = core.trend_flags_sma200(close, lb, modeId, epsPpb, lenAtr, thetaNorm)

// 確定足ゲート
bool ok = confirmedOn ? barstate.isconfirmed : true

// ヒステリシス（純関数・未使用引数なし）
var int state = 0     // 1=UP, 0=FLAT, -1=DOWN
int next = ok ? core.next_trend_state(state, slopeP, norm, modeId, epsPpb, thetaNorm, hyst) : state
state := next

// --- draw（グローバルのみ） ---
// 改行はスペース4以外で！
plot(core.sma_val(close, 200), 
     title="SMA200", 
     color=color.new(DEF_SMA_COLOR, 50),   // ← 透過50%
     linewidth=DEF_SMA_WIDTH
     )
barcolor(state == 1  ? DEF_UP_COLOR    : na)
barcolor(state == -1 ? DEF_DOWN_COLOR  : na)

plotchar(state==1  ? 1 : na, title="UP",   char='U', location=location.top,  color=DEF_UP_COLOR)
plotchar(state==-1 ? 1 : na, title="DOWN", char='D', location=location.top,  color=DEF_DOWN_COLOR)
plotchar(state==0  ? 1 : na, title="FLAT", char='F', location=location.bottom,       color=DEF_FLAT_COLOR)


// --- debug（必要時のみON） ---
// plot(slopeP, title="slope (pips/bar)")
// plot(norm,   title="slope/ATR")